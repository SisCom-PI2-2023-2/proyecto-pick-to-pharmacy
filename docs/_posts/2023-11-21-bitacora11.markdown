---
layout: post
title: Semana 11
categories: posts
---

En la semana del 21 y el 23 de noviembre, definimos las últimas cosas para los códigos y posteriormente terminar y armar la maqueta para la siguiente semana. Agregamos al codigo de arduino los componentes que nos faltaban y adaptamos lo mismo en la otra placa.

El codigo de la esp8266 quedaria de la siguiente manera:  

```c++

/*
 Basic ESP8266 MQTT + Thingsboard Example
 Based on MQTT ESP8266 example
 Andres Ferragut
 ferragut@fi365.ort.edu.uy
*/
#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <ArduinoJson.h>
#include <MFRC522.h>   //GithubCommunity Version 1.4.10
#include "Wire.h"
#include <LiquidCrystal_I2C.h>



// Update these with values suitable for your network.
const char* ssid = "HUAWEI-IoT";
const char* password = "ORTWiFiIoT";
const char* mqtt_server = "demo.thingsboard.io";
const char* token = "xtYELlImXmVGtgtR2JBt";
#define RST_PIN  0   //Pin 9 para el reset del RC522
#define SS_PIN  15   //Pin 10 para el SS (SDA) del RC522
MFRC522 mfrc522(SS_PIN, RST_PIN); //Creamos el objeto para el RC522
byte newUID[4];
int ultimoEstadoLed = 0;
int estado_servidor = 0;
String ibu = "";
String ome = "";
String asp = "";
String nov = "";
// Connection objects
WiFiClient espClient;
PubSubClient client(espClient);
//timestamp of the last telemetry update
unsigned long lastMsg = 0;
//buffer sizes and messages
#define MSG_BUFFER_SIZE  (50)
char msg[MSG_BUFFER_SIZE];
char msg2[MSG_BUFFER_SIZE];
//json document to store incoming messages
DynamicJsonDocument incoming_message(256);
DynamicJsonDocument Parametros(256);
//Fake telemetry
int value = 0;
//Variables debounce boton
#define boton 16

//LCDs
LiquidCrystal_I2C LCDibu(0x27,16,2);
LiquidCrystal_I2C LCDomeo(0x26,16,2);
LiquidCrystal_I2C LCDaspi(0x25,16,2);
LiquidCrystal_I2C LCDnove(0x23,16,2);

int buttonState;            // the current reading from the input pin
int lastButtonState = LOW;  // the previous reading from the input pin

// the following variables are unsigned longs because the time, measured in
// milliseconds, will quickly become a bigger number than can be stored in an int.
unsigned long lastDebounceTime = 0;  // the last time the output pin was toggled
unsigned long debounceDelay = 50;    // the debounce time; increase if the output flickers
//Initiates WiFi Connection
void setup_wifi() {
  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("connecting to ");
  Serial.println(ssid);
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  randomSeed(micros());
  Serial.println("");
  Serial.println("wifi connected");
  Serial.println("ip address: ");
  Serial.println(WiFi.localIP());
}
//This method is called whenever a MQTT message arrives. We must be prepared for any type of incoming message.
//We are subscribed to RPC Calls: v1/devices/me/rpc/request/+
void callback(char* topic, byte* payload, unsigned int length) {
  //log to console
  //Serial.print("Message arrived [");
  //Serial.print(topic);
  //Serial.print("] ");
  //for (int i = 0; i < length; i++) {
    //Serial.print((char)payload[i]);
  //}
  //Serial.println();
  //convert topic to string to parse
  String _topic = String(topic);
  
  if (_topic.startsWith("v1/devices/me/rpc/request/")) {
    //We are in a request, check request number
    String _number = _topic.substring(26);
    //Read JSON Object
    deserializeJson(incoming_message, payload);
    String metodo = incoming_message["method"];
    
    if (metodo=="checkStatus") {  //Check device status. Expects a response to the same topic number with status=true.
      
      char outTopic[128];
      ("v1/devices/me/rpc/response/"+_number).toCharArray(outTopic,128);
      
      DynamicJsonDocument resp(256);
      resp["status"] = true;
      char buffer[256];
      serializeJson(resp, buffer);
      client.publish(outTopic, buffer);
    }
    
    if (metodo=="AbrirCompuertas") {  //Set led status and update attribute value via MQTT
        if(ibu != "0"){
          Serial.print("W");
        }
        if(ome != "0"){
          Serial.print("X");
        }
        if(asp != "0"){
          Serial.print("Y");
        }
        if(nov != "0"){
          Serial.print("Z");
        }
      
   
      String localResponseTopic = String(topic);
      localResponseTopic.replace("request", "response");  //change topic to preserver requestID
      client.publish(localResponseTopic.c_str(), "{\"Estado\": 2}");
      estado_servidor = 2;
    }
    if (metodo=="CerrarCompuertas") {  //Set led status and update attribute value via MQTT

      LCDibu.setCursor(1, 1);
      LCDibu.print("              ");
      LCDibu.noDisplay();

      LCDomeo.setCursor(1, 1);
      LCDomeo.print("              ");
      LCDomeo.noDisplay();

      LCDaspi.setCursor(1, 1);
      LCDaspi.print("              ");
      LCDaspi.noDisplay();

      LCDnove.setCursor(1, 1);
      LCDnove.print("              ");
      LCDnove.noDisplay();

      String localResponseTopic = String(topic);
      localResponseTopic.replace("request", "response");  //change topic to preserver requestID
      client.publish(localResponseTopic.c_str(), "{\"Estado\": 0}");
      estado_servidor = 0;
    }
    if (metodo=="PrenderLeds") {
      ibu = String(incoming_message["params"]["Ibuprofeno"]);
      ome = String(incoming_message["params"]["Omeoprazol"]);
      asp = String(incoming_message["params"]["Aspirina"]);
      nov = String(incoming_message["params"]["Novemina"]);

      if(ibu != "0"){
        Serial.print("I");
        LCDibu.setCursor(8, 1);
        LCDibu.print("CANT: "+ibu);
        LCDibu.display();
      }
      if(ome != "0"){
        Serial.print("O");
        LCDomeo.setCursor(8, 1);
        LCDomeo.print("CANT: "+ome);
        LCDomeo.display();
      }
      if(asp != "0"){
        Serial.print("A");
        LCDaspi.setCursor(8, 1);
        LCDaspi.print("CANT: "+asp);
        LCDaspi.display();
      }
      if(nov != "0"){
        Serial.print("N");
        LCDnove.setCursor(8, 1);
        LCDnove.print("CANT: "+nov);
        LCDnove.display();
      }


      //Serial.println("Ibuprofeno: "+ ibu);
      //Serial.println("Omeoprazol: "+ ome);
      //Serial.println("Aspirina: "+ asp);
      //Serial.println("Novemina: "+ nov);
      String localResponseTopic = String(topic);
      localResponseTopic.replace("request", "response");  //change topic to preserver requestID
      client.publish(localResponseTopic.c_str(), "{\"Estado\": 1}");
      estado_servidor = 1;
    }

    if (metodo=="CorregirError") {

      String erribu = String(incoming_message["params"]["ErrIbu"]);
      String errome = String(incoming_message["params"]["ErrOmeo"]);
      String errasp = String(incoming_message["params"]["ErrAspi"]);
      String errnov = String(incoming_message["params"]["ErrNove"]);

      if(erribu != "0"){
        LCDibu.setCursor(1, 1);
        LCDibu.print("Error al sacar");
      }
      if(errome != "0"){
        LCDomeo.setCursor(1, 1);
        LCDibu.print("Error al sacar");
      }
      if(errasp != "0"){
        LCDaspi.setCursor(1, 1);
        LCDibu.print("Error al sacar");
      }
      if(errnov != "0"){
        LCDnove.setCursor(1, 1);
        LCDibu.print("Error al sacar");
      }

      String localResponseTopic = String(topic);
      localResponseTopic.replace("request", "response");  //change topic to preserver requestID
      client.publish(localResponseTopic.c_str(), "{\"Estado\": 2}");
    }
  }
}
void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("attempting mqtt connection...");
    // Attempt to connect - client.connect(DEVICE_ID, TOKEN, TOKEN)
    if (client.connect("nodemcu nuevo", token, token )) {
      Serial.println("connected");
      // Once connected, subscribe to rpc topic
      client.subscribe("v1/devices/me/rpc/request/+");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}
void setup() {
  pinMode(BUILTIN_LED, OUTPUT);     // Initialize the BUILTIN_LED pin as an output
  Serial.begin(9600);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
  bool estado = false;
  SPI.begin();        //Iniciamos el Bus SPI
  mfrc522.PCD_Init(); // Iniciamos  el MFRC522
  pinMode(16,INPUT);

  LCDibu.init();
  LCDibu.backlight();
  LCDibu.setCursor(1, 0);
  LCDibu.print("IBUPROFENO");

  LCDomeo.init();
  LCDomeo.backlight();
  LCDomeo.setCursor(1, 0);
  LCDomeo.print("OMEOPRAZOL");

  LCDaspi.init();
  LCDaspi.backlight();
  LCDaspi.setCursor(1, 0);
  LCDaspi.print("ASPIRINA");

  LCDnove.init();
  LCDnove.backlight();
  LCDnove.setCursor(1, 0);
  LCDnove.print("NOVEMINA");
}


void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();
  if (estado_servidor == 1){
    verificarRFID();
  }


  if (estado_servidor == 2){
     DeobunceBoton();


  }


}
void verificarRFID(){
  String myarray = "";

 if ( mfrc522.PICC_IsNewCardPresent()) {
    //Seleccionamos una tarjeta
    if ( mfrc522.PICC_ReadCardSerial())
    {
      // Enviamos serialemente su UID
      for (int i = 0; i < mfrc522.uid.size; i++) {
        newUID[i] = mfrc522.uid.uidByte[i];
      }
      // Terminamos la lectura de la tarjeta  actual
      for (int i = 0; i < mfrc522.uid.size; i++) {
        //Serial.print(newUID[i], HEX);
        myarray += String(newUID[i], HEX);
      }
      Serial.println();
      mfrc522.PICC_HaltA();
    }
    //Serial.println(myarray);
    //Send value as telemetry
    DynamicJsonDocument resp(256);
    resp["nuevaTarjeta"] = myarray;
    char buffer[256];
    serializeJson(resp, buffer);
    
    client.publish("v1/devices/me/attributes", buffer);
    
    //Serial.print("publish attributes: ");
    //Serial.println(buffer);
    //Serial.print(" ");
  }   
}
void DeobunceBoton(){
  int reading = digitalRead(boton);
  // check to see if you just pressed the button
  // (i.e. the input went from LOW to HIGH), and you've waited long enough
  // since the last press to ignore any noise:
  // If the switch changed, due to noise or pressing:
  if (reading != lastButtonState) {
    // reset the debouncing timer
    lastDebounceTime = millis();
  }
  if ((millis() - lastDebounceTime) > debounceDelay) {
    // whatever the reading is at, it's been there for longer than the debounce
    // delay, so take it as the actual current state:
    
    // if the button state has changed:
    if (reading != buttonState) {
      buttonState = reading;

      // only toggle the LED if the new button state is HIGH
      if (buttonState == LOW) {
        VerificarStock();
      }
    }
  }
  lastButtonState = reading;
}
void VerificarStock(){
  DynamicJsonDocument resp(256);

  int contador = 0;
  int cantidades[4];

  Serial.print("M");
  while (contador < 4){
    if (Serial.available()){
      cantidades[contador] = Serial.read();

      contador++;
    }
  }
  resp["CStockIbu"]  = cantidades[0] - 48;
  resp["CStockOmeo"] = cantidades[1] - 48;
  resp["CStockAspi"] = cantidades[2] - 48;
  resp["CStockNove"] = cantidades[3] - 48;

  char buffer[256];
  serializeJson(resp, buffer);
  
  client.publish("v1/devices/me/attributes", buffer);
}

```

Y el código de la Arduino Mega de la siguiente manera:  

```c++

#include <Servo.h>
#define Led1 3
#define Led2 4
#define Led3 5
#define Led4 6
#define trigIbu 22
#define echoIbu 30
#define trigOmeo 23
#define echoOmeo 31
#define trigAspi 24
#define echoAspi 32
#define trigNove 25
#define echoNove 33
Servo ServoIbu;
Servo ServoOmeo;
Servo ServoAspi;
Servo ServoNove;
float espacio[4] = {2, 2, 2, 2};
float grosor[4] = {2, 2, 2, 2};
int cantMax[4] = {10, 10, 10, 10};
void setup() {
  Serial.begin(9600);
  pinMode(Led1, OUTPUT);
  pinMode(Led2, OUTPUT);
  pinMode(Led3, OUTPUT);
  pinMode(Led4, OUTPUT);
  ServoIbu.attach(13);
  ServoOmeo.attach(12);
  ServoAspi.attach(11);
  ServoNove.attach(10);
  pinMode(trigIbu, OUTPUT);
  pinMode(echoIbu, INPUT);
  pinMode(trigOmeo, OUTPUT);
  pinMode(echoOmeo, INPUT);
  pinMode(trigAspi, OUTPUT);
  pinMode(echoAspi, INPUT);
  pinMode(trigNove, OUTPUT);
  pinMode(echoNove, INPUT);
}
void loop() {
    if(Serial.available()){
      int lectura = Serial.read();
      if(lectura == 'I'){
        digitalWrite(Led1, HIGH);
      }
      else if(lectura == 'O'){
        digitalWrite(Led2, HIGH);
      }
      else if(lectura == 'A'){
        digitalWrite(Led3, HIGH);
      }
      else if(lectura == 'N'){
        digitalWrite(Led4, HIGH);
      }
      else if(lectura == 'W'){
        ServoIbu.write(180);
      }
      else if(lectura == 'X'){
        ServoOmeo.write(180);
      }
      else if(lectura == 'Y'){
        ServoAspi.write(180);
      }
      else if(lectura == 'Z'){
        ServoNove.write(180);
      }
      else if(lectura == 'C'){
        ServoIbu.write(0);
        ServoOmeo.write(0);
        ServoAspi.write(0);
        ServoNove.write(0);
        digitalWrite(Led1, LOW);
        digitalWrite(Led2, LOW);
        digitalWrite(Led3, LOW);
        digitalWrite(Led4, LOW);
      }
      else if(lectura == 'M'){
        for(int i=0; i<4;i++){
        // Clears the trigPin
        digitalWrite(trigIbu + i, LOW);
        delayMicroseconds(2);
        // Sets the trigPin on HIGH state for 10 micro seconds
        digitalWrite(trigIbu + i, HIGH);
        delayMicroseconds(10);
        digitalWrite(trigIbu + i, LOW);
        // Reads the echoPin, returns the sound wave travel time in microseconds
        int duration = pulseIn(echoIbu + i, HIGH);
        // Calculating the distance
        int distance = duration * 0.034 / 2;
        int cantidad = cantMax[i] - int(round((distance - espacio[i])/grosor[i]));
        Serial.print(cantidad);
        }
      }
    }
}

```


![ArduinoMega](/proyecto-pick-to-pharmacy/assets/ArduinoMega.png)


![Empezando](/proyecto-pick-to-pharmacy/assets/Empezando.png) 



